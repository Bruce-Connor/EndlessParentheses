---
title: "Provide input to the compilation buffer"
date: 2016-06-09
layout: post
tags: compilation programming init.el
---
<p>
The Emacs <a href="http://doc.endlessparentheses.com/Fun/compile"><code>compile</code></a> command is a severely underused tool. It allows you to run
any build tool under the sun and provides error-highlighting and jump-to-error
functionality for dozens of programming languages, but many an Emacser is still
in the habit of switching to a terminal in order to run <code>make</code>, <code>lein test</code>, or
<code>bundle exec</code>. It does have one limitation, though. The compilation buffer is
not a real shell, so if the command being run asks for user input (even a simple
<code>y/n</code> confirmation) there’s no way to provide it.
</p>

<p>
<i>(Since posting this, I’ve learned that part of it is mildly futile. Read the
update below for more information.)</i>
</p>

<p>
Fortunately, that’s not hard to fix. The snippet below defines two commands. The
first one prompts you for input and then sends it to the underlying terminal
followed by a newline, designed for use with prompts and momentary REPLs. The
second is a command that simply sends the key that was pressed to invoke it,
designed for easily replying to <code>y/n</code> questions or quickly quitting REPLs with
<kbd>C-d</kbd> or <kbd>C-j</kbd>.
</p>
{% highlight cl %}
(defun endless/send-input (input &optional nl)
  "Send INPUT to the current process.
Interactively also sends a terminating newline."
  (interactive "MInput: \nd")
  (let ((string (concat input (if nl "\n"))))
    ;; This is just for visual feedback.
    (let ((inhibit-read-only t))
      (insert-before-markers string))
    ;; This is the important part.
    (process-send-string
     (get-buffer-process (current-buffer))
     string)))

(defun endless/send-self ()
  "Send the pressed key to the current process."
  (interactive)
  (endless/send-input
   (apply #'string
          (append (this-command-keys-vector) nil))))

(dolist (key '("\C-d" "\C-j" "y" "n"))
  (define-key compilation-mode-map key
    #'endless/send-self))
{% endhighlight %}
<p>
This is something I’ve run into for years, but I finally decided to fix it
because it meant I couldn’t run Ruby’s <code>rspec</code> in the compilation buffer if my
code contained a <code>binding.pry</code> (which spawns a REPL). Now I can actually interact
with this REPL via <kbd>C-c i</kbd> or just quickly get rid of it with <kbd>C-d</kbd>. If you run
into the same situation, you should also set the following option in your
<code>.pryrc</code> file.
</p>
{% highlight ruby %}
Pry.config.pager = false if ENV["INSIDE_EMACS"]
{% endhighlight %}

<div id="outline-container-orgheadline1" class="outline-2">
<h2 id="orgheadline1">Update <span class="timestamp-wrapper"><span class="timestamp">17 Jun 2016</span></span></h2>
<div class="outline-text-2" id="text-orgheadline1">
<p>
As Clément points out in the comments, you can run compilation commands in
<code>comint-mode</code> by providing the <kbd>C-u</kbd> prefix to <kbd>M-x</kbd> <code>compile</code>. You still have
all of the usual <code>compilation-mode</code> features (like <code>next/previous-error</code>), with
the additional benefit that the buffer accepts input like a regular shell does.
</p>

<p>
The only caveat is that, since the buffer is modifiable, you lose some
convenience keys like <kbd>q</kbd> to quit the buffer or <kbd>g</kbd> to recompile, so you’ll need
to bind them somewhere else
</p>
{% highlight cl %}
(define-key compilation-minor-mode-map (kbd "<f5>")
  #'recompile)
(define-key compilation-minor-mode-map (kbd "<f9>")
  #'quit-window)

(define-key compilation-shell-minor-mode-map (kbd "<f5>")
  #'recompile)
(define-key compilation-shell-minor-mode-map (kbd "<f9>")
  #'quit-window)
{% endhighlight %}

<p>
I still like it that the previous solution gives me quick access to <kbd>C-d</kbd> and
<kbd>y/n</kbd> for those cases when I forget to use <code>comint-mode</code>, but the solution I had
for inputting long strings is definitely redundant now. Instead, we can have a
key that restarts the current compilation in <code>comint-mode</code>.
</p>
{% highlight cl %}
(require 'cl-lib)
(defun endless/toggle-comint-compilation ()
  "Restart compilation with (or without) `comint-mode'."
  (interactive)
  (cl-callf (lambda (mode) (if (eq mode t) nil t))
      (elt compilation-arguments 1))
  (recompile))

(define-key compilation-mode-map (kbd "C-c i")
  #'endless/toggle-comint-compilation)
(define-key compilation-minor-mode-map (kbd "C-c i")
  #'endless/toggle-comint-compilation)
(define-key compilation-shell-minor-mode-map (kbd "C-c i")
  #'endless/toggle-comint-compilation)
{% endhighlight %}
</div>
</div>
