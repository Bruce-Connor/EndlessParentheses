---
title: "Updating org-mode #+INCLUDE: statements on the fly"
date: 2014-09-26
layout: post
tags: org-mode emacs
series: "Emacs.SE Lessons"
---

<p>
Today’s post regards <a href="http://emacs.stackexchange.com/a/246/50">my answer</a> to <a href="http://emacs.stackexchange.com/q/64/50">kaushalmodi's question</a>. Since the
beta is <a href="/emacs-stack-exchange-enters-beta.html">still private</a>, you might not be able to follow those links
quite yet, so I’ll summ it up here.
</p>

<div id="outline-container-sec-1" class="outline-2">
<h2 id="sec-1">The Question</h2>
<div class="outline-text-2" id="text-1">
<p>
Kaushalmodi uses <code>#+INCLUDE:</code> statements with line specifications in his
org files. Here is an example similar to his. 14 and 80 are the first
and last line of a class declaration in the source file, so they’re
quite obvious for a human to identify.
</p>
{% highlight text %}
#+INCLUDE: "code/my-class.sv" :src systemverilog :lines "14-80"
{% endhighlight %}

<p>
The problem here is that whenever <i>“my-class.sv”</i> is edited those
line numbers are likely to become outdated. So you would have to go
through each org file which might include <i>“my-class.sv”</i> and update
the numbers.
</p>
</div>
</div>

<div id="outline-container-sec-2" class="outline-2">
<h2 id="sec-2">The Solution</h2>
<div class="outline-text-2" id="text-2">
<p>
Unfortunately, org-mode doesn’t have a flexible way of declaring
include statements. You either specify the line numbers or you don’t.
</p>

<p>
The <a href="http://emacs.stackexchange.com/a/286/50">solution was</a> to add <code>:range-begin</code> and <code>:range-end</code> keywords to
the statement
</p>
{% highlight text %}
#+INCLUDE: "code/my-class.sv" :src systemverilog :range-begin "^class" :range-end "^endclass" :lines "14-80"
{% endhighlight %}
<p>
and then write a function which
</p>
<ol class="org-ol">
<li>goes through each <code>#+INCLUDE:</code> statement in the buffer,
</li>
<li>checks if it has <code>:range-begin</code> and/or <code>:range-end</code> keywords and
takes their arguments as regular expressions,
</li>
<li>visits the relevant file and searches for these regular expressions,
</li>
<li>checks what the line numbers are now,
</li>
<li>and updates them accordingly in the org buffer.
</li>
</ol>

<p>
This function can then be assigned to a key, added to
<a href="https://doc.endlessparentheses.com/Var/before-save-hook"><code>before-save-hook</code></a>, or added to one of org-mode’s bajillion available
hooks. Finally, to go the extra mile, we make the behaviour
customizable per file extension through a <code>defcustom</code>.
</p>
{% highlight cl %}
(add-hook 'before-save-hook #'endless/update-includes)

(defun endless/update-includes (&rest ignore)
  "Update the line numbers of #+INCLUDE:s in current buffer.
Only looks at INCLUDEs that have either :range-begin or :range-end.
This function does nothing if not in org-mode, so you can safely
add it to `before-save-hook'."
  (interactive)
  (when (derived-mode-p 'org-mode)
    (save-excursion
      (goto-char (point-min))
      (while (search-forward-regexp
              "^\\s-*#\\+INCLUDE: *\"\\([^\"]+\\)\".*:range-\\(begin\\|end\\)"
              nil 'noerror)
        (let* ((file (expand-file-name (match-string-no-properties 1)))
               lines begin end)
          (forward-line 0)
          (when (looking-at "^.*:range-begin *\"\\([^\"]+\\)\"")
            (setq begin (match-string-no-properties 1)))
          (when (looking-at "^.*:range-end *\"\\([^\"]+\\)\"")
            (setq end (match-string-no-properties 1)))
          (setq lines (endless/decide-line-range file begin end))
          (when lines
            (if (looking-at ".*:lines *\"\\([-0-9]+\\)\"")
                (replace-match lines :fixedcase :literal nil 1)
              (goto-char (line-end-position))
              (insert " :lines \"" lines "\""))))))))

(defun endless/decide-line-range (file begin end)
  "Visit FILE and decide which lines to include.
BEGIN and END are regexps which define the line range to use."
  (let (l r)
    (save-match-data
      (with-temp-buffer
        (insert-file file)
        (goto-char (point-min))
        (if (null begin)
            (setq l "")
          (search-forward-regexp begin)
          (setq l (line-number-at-pos (match-beginning 0))))
        (if (null end)
            (setq r "")
          (search-forward-regexp end)
          (setq r (1+ (line-number-at-pos (match-end 0)))))
        (format "%s-%s" l r)))))
{% endhighlight %}
</div>
</div>
