---
title: "Ispell and Abbrev, the Perfect Auto-Correct"
date: 2014-07-05
layout: post
tags: ispell abbrev writing keybind init.el emacs
meta_title: "Emacs, Ispell and Abbrev, the Perfect Auto-Correct"
---
<p>
I am not a fantastic typist. My speed is acceptable, but I make a great
deal of mistakes. The following snippet has turned me into the Messi
of keyboards.
</p>

<p>
Whenever I make a typo:
</p>

<ol class="org-ol">
<li>Hit <kbd>C-x C-i</kbd>, instead of erasing the mistake;</li>
<li>Select the appropriate correction (thanks to Ispell);</li>
<li>Sleep easier at night knowing I'll never see that mistake again (thanks to abbrev).</li>
</ol>

{% highlight cl %}
(define-key ctl-x-map "\C-i"
  #'endless/ispell-word-then-abbrev)

(defun endless/simple-get-word ()
  (car-safe (save-excursion (ispell-get-word nil))))

(defun endless/ispell-word-then-abbrev (p)
  "Call `ispell-word', then create an abbrev for it.
With prefix P, create local abbrev. Otherwise it will
be global.
If there's nothing wrong with the word at point, keep
looking for a typo until the beginning of buffer. You can
skip typos you don't want to fix with `SPC', and you can
abort completely with `C-g'."
  (interactive "P")
  (let (bef aft)
    (save-excursion
      (while (if (setq bef (endless/simple-get-word))
                 ;; Word was corrected or used quit.
                 (if (ispell-word nil 'quiet)
                     nil ; End the loop.
                   ;; Also end if we reach `bob'.
                   (not (bobp)))
               ;; If there's no word at point, keep looking
               ;; until `bob'.
               (not (bobp)))
        (backward-word)
        (backward-char))
      (setq aft (endless/simple-get-word)))
    (if (and aft bef (not (equal aft bef)))
        (let ((aft (downcase aft))
              (bef (downcase bef)))
          (define-abbrev
            (if p local-abbrev-table global-abbrev-table)
            bef aft)
          (message "\"%s\" now expands to \"%s\" %sally"
                   bef aft (if p "loc" "glob")))
      (user-error "No typo at or before point"))))

(setq save-abbrevs 'silently)
(setq-default abbrev-mode t)
{% endhighlight %}
<p>
In my innocence, I had initially assigned this command to a
3-key sequence. It took me less then a month to realise it needed a
shorter bind, I'd been using it 30 times a day.
</p>

<p>
Auto correction with abbrev is <a href="http://www.emacswiki.org/emacs/autocorrection_abbrev_defs">far from a new concept</a>. Do you use
anything similar?
</p>

<div id="outline-container-orgheadline1" class="outline-2">
<h2 id="orgheadline1">Update <span class="timestamp-wrapper"><span class="timestamp">11 Jan 2016</span></span></h2>
<div class="outline-text-2" id="text-orgheadline1">
<p>
The command now searches backward for the closest wrong word. So you can just
hit <kbd>C-x C-i</kbd> even if the mistake happened several words ago.
</p>
</div>
</div>

<div id="outline-container-orgheadline2" class="outline-2">
<h2 id="orgheadline2">Update <span class="timestamp-wrapper"><span class="timestamp">20 Jan 2016</span></span></h2>
<div class="outline-text-2" id="text-orgheadline2">
<p>
Fixed some corner-case issues with the previous update.</p>
</div>
</div>
