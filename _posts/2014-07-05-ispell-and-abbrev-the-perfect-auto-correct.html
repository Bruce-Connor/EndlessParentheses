---
title: "Ispell and Abbrev, the Perfect Auto-Correct"
date: 2014-07-05
layout: post
tags: ispell abbrev writing keybind init.el
meta_title: "Emacs, Ispell and Abbrev, the Perfect Auto-Correct"
---
<p>
I am not a fantastic typist. My speed is acceptable, but I make a great
deal of mistakes. The following snippet has turned me into the Messi
of keyboards.
</p>

<p>
Whenever I make a typo:
</p>

<ol class="org-ol">
<li>Hit <kbd>C-x C-i</kbd>, instead of erasing the mistake;</li>
<li>Select the appropriate correction (thanks to Ispell);</li>
<li>Sleep easier at night knowing I'll never see that mistake again (thanks to abbrev).</li>
</ol>

{% highlight cl %}
(define-key ctl-x-map "\C-i"
  #'endless/ispell-word-then-abbrev)

(defun endless/ispell-word-then-abbrev (p)
  "Call `ispell-word', then create an abbrev for it.
With prefix P, create local abbrev. Otherwise it will
be global."
  (interactive "P")
  (let (bef aft)
    (save-excursion
      (while (progn
               (backward-word)
               (and (setq bef (thing-at-point 'word))
                    (not (ispell-word nil 'quiet)))))
      (setq aft (thing-at-point 'word)))
    (when (and aft bef (not (equal aft bef)))
      (setq aft (downcase aft))
      (setq bef (downcase bef))
      (define-abbrev
        (if p local-abbrev-table global-abbrev-table)
        bef aft)
      (message "\"%s\" now expands to \"%s\" %sally"
               bef aft (if p "loc" "glob")))))

(setq save-abbrevs 'silently)
(setq-default abbrev-mode t)
{% endhighlight %}
<p>
In my innocence, I had initially assigned this command to a
3-key sequence. It took me less then a month to realise it needed a
shorter bind, I'd been using it 30 times a day.
</p>

<p>
Auto correction with abbrev is <a href="http://www.emacswiki.org/emacs/autocorrection_abbrev_defs">far from a new concept</a>. Do you use
anything similar?
</p>

<div id="outline-container-orgheadline1" class="outline-2">
<h2 id="orgheadline1">Update <span class="timestamp-wrapper"><span class="timestamp">&lt;11 Jan 2016&gt;</span></span></h2>
<div class="outline-text-2" id="text-orgheadline1">
<p>
The command now searches backward for the closest wrong word. So you can just
hit <kbd>C-x C-i</kbd> even if the mistake happened several words ago.
</p>
</div>
</div>
